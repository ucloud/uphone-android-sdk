plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"
    //配置keystore签名
    signingConfigs {
        release {
            storeFile file('demo.jks')
            keyAlias '123456'
            keyPassword '123456'
            storePassword '123456'
        }
    }
    defaultConfig {
        applicationId "com.ucloud.demo"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //指定打包后应用名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                outputFileName = "UPhoneAndroidSdkDemo_${defaultConfig.versionName}_alpha_${releaseTime()}.apk"
            } else if (variant.buildType.name == 'debug') {
                outputFileName = "UPhoneAndroidSdkDemo_${defaultConfig.versionName}_debug_${releaseTime()}.apk"
            }
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
    sourceSets {
        main {
            // The libs directory contains prebuilt libraries that are used by the
            // app's library defined in CMakeLists.txt via an IMPORTED target.
            jniLibs.srcDirs = ['libs']
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.+'
    implementation (name: 'uphonesdk_1.0.5', ext: 'aar')
    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation "com.google.code.gson:gson:2.8.2"
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    implementation 'com.google.protobuf:protobuf-java:4.0.0-rc-2'
}